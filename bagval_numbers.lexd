PATTERN decimal
rootDec dec

PATTERN centi
rootCent cent

PATTERN milli
rootCent mil

PATTERN singles
rootInit[DaNumber] end[DaNumber]
rootInit[RaNumber] end[RaNumber] # ра, Кибрик et al. С. 155

PATTERN DaSuffixedNums
decimal end[DaNumber]
milli end[DaNumber]
centi end[DaNumber]

PATTERN NomNumbersAgr
singles ( OblSingular[OblSu] | OblPlural[OblPlLu] )
DaSuffixedNums ( OblSingular[OblSu] | OblPlural[OblPlLu] )

PATTERN NomNumbersNonAgr
singles OblSingular[OblLi]
DaSuffixedNums OblSingular[OblLi]

PATTERNS
# regular numbers
#
rootOne agrSingular
singles
decimal # аца
centi # бешен
DaSuffixedNums
#
#forms with links
#
decimal decComp # ацал, link in 11-19
rootDec conjDec # ац, link in 21-29
centi conjCentMil  
milli conjCentMil # links in 101-199, 10001-1999
#
#collective forms
#
singles col 
decimal col 
DaSuffixedNums col
#
#distributional forms
#
rootCent centDist cent end[DaNumber]
rootCent milDist mil end[DaNumber]
decDist dec ( end[DaNumber] | end[RaNumber] )
#
#nominalised numbers
#
singles DatExceptions[-OblPlAlu]
DaSuffixedNums DatExceptions[-OblPlAlu]
( NomNumbersAgr | NomNumbersNonAgr ) CaseMain[-RegDat,ErgR]
NomNumbersAgr genitiveCase[GenAgr] 
NomNumbersNonAgr genitiveCase[-GenAgr]

# LEXICON ENTRIES
# number suffixes
# 
LEXICON end
<num>:да[DaNumber]
<num>:ра[RaNumber]

LEXICON col
<col>:ла

# Oblique stem markers
#
LEXICON OblSingular
<sg><obl>:а[OblA]
<sg><obl>:и[OblI]
<sg><obl>:е[OblE]
<sg><obl>:у[OblU]
<sg><obl>:шу[OblSu]
<sg><obl>:лу[OblLu]
<sg><obl>:лъи[OblLi]

LEXICON OblPlural
<obl>:а[OblPlA]
<obl>:алу[OblPlAlu]
<obl>:лу[OblPlLu]

# Noun plural suffixes
#
LEXICON PlAbs
<pl>:аби

LEXICON PlObl
<pl>:аб # for oblique stems

# case markers
# 
LEXICON CaseMain
<dat>:ла[ErgD,ErgR,RegDat]
<erg>:д[ErgD]
<erg>:р[ErgR]
<aff>:ба[ErgD,ErgR]
<subst>:бас[ErgD,ErgR]

LEXICON DatExceptions
<sg><obl><dat>:ша[ErgD,ErgR,OblSu]
<sg><obl><dat>:лъа[ErgD,ErgR,OblLi]
<pl><obl><dat>:лла[ErgD,ErgR,OblPlAlu]
<pl><obl><dat>:ла[ErgD,ErgR,OblPlLu]

LEXICON genitiveCase
<gen>:лъ
<gen><M><sg>:в[GenAgr]
<gen><F><sg>:й[GenAgr]
<gen><N><sg>:б[GenAgr]
<gen><H><pl>:ба[GenAgr]
<gen><N><pl>:р[GenAgr]

# agreement markers
# 
LEXICON agrSingular
<M><sg>:в
<F><sg>:й
<N><sg>:б

LEXICON agrPlural
<H><pl>:ба
<N><pl>:р

# number modifiers
# 
LEXICON dec
<*>гьаца<num>:ацӀа

LEXICON conjDec
<*>гьаца<num>:ацӀ # 21-29

LEXICON cent
<*>бешенда<num><hundred>:бешен

LEXICON mil
<*>азарда<num><thousand>:азар

# distributional modifiers
#
LEXICON decDist
цев<num><distr>:гьацӀигь

LEXICON centDist
<distr>:беши

LEXICON milDist
<distr>:ази

# number links
# 
LEXICON decComp
<lnk>:л # 11-19

LEXICON conjCentMil
<lnk>:чӀекӀуб

# number roots
# 
LEXICON rootOne
цев<num>:це # 1

LEXICON rootInit
чӀеда<num>:чӀ{е}[DaNumber,RaNumber]
гьабда<num>:гьаб[DaNumber]
буъуда<num>:буъу[DaNumber,RaNumber]
инщтуда<num>:инщту[DaNumber,RaNumber]
инлӀида<num>:инлӀи[DaNumber,RaNumber]
гьакьуда<num>:гьакьу[DaNumber,RaNumber]
бикьида<num>:бикьи[DaNumber,RaNumber]
гьачӀвада<num>:гьачӀва[DaNumber,RaNumber]

LEXICON rootDec
цев<num>:гь
чӀеда<num>:чӀ{е} # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
инщтуда<num>:инщвд # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀв # 9

LEXICON rootCent
цев<num>:
чӀеда<num>:чӀ{е} # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
инщтуда<num>:инщв # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀа # 9

# noun roots
#
LEXICON nounSu
има<noun>:има # father

LEXICON nounLi
гьакӀуй<noun>:гьакӀуй # woman
гьезеб<noun>:гьезеб # stock