PATTERN decimal
rootDec dec

PATTERN centi
rootCent cent

PATTERN milli
rootCent mil

PATTERN singles
rootInit[DaNumber] end[DaNumber]
rootInit[RaNumber] end[RaNumber] # ра, Кибрик et al. С. 155

PATTERN DaSuffixedNums
decimal end[DaNumber]
milli end[DaNumber]
centi end[DaNumber]

PATTERN NomNumbersAgr
singles ( OblSingular[OblSu] | OblPlural[OblPlLu] )
DaSuffixedNums ( OblSingular[OblSu] | OblPlural[OblPlLu] )

PATTERN NomNumbersNonAgr
singles OblSingular[OblLi]
DaSuffixedNums OblSingular[OblLi]

# ----------------------------------------------------------------------------------------------
# ----------------------------------------------------------------------------------------------
# MAIN PATTERNS
PATTERNS
# regular numbers
#
rootOne[norm] agrFinal
singles
decimal # аца
centi # бешен
DaSuffixedNums
#
#forms with links
#
decimal decComp # ацал, link in 11-19
rootDec conjDec # ац, link in 21-29
centi conjCentMil  
milli conjCentMil # links in 101-199, 10001-1999
#
#collective numbers
#
singles col 
decimal col 
DaSuffixedNums col
#
#ordinal numbers
#
singles [ла]? ordinal[ser1] agrFinal 
singles [ла] ordinal[ser2]
DaSuffixedNums [ла]? ordinal[ser1] agrFinal 
DaSuffixedNums [ла] ordinal[ser2]
#
#distributional numbers
#
rootOne[red] agrFinal # цецеб = 1
rootCent centDist cent end[DaNumber]
rootCent milDist mil end[DaNumber]?
decDist dec ( end[DaNumber] | end[RaNumber] ) # гьабдигьабда = 10
2xRootInit[excl] # 2
2xRootInit[-excl,DaNumber] end[DaNumber]
2xRootInit[-excl,RaNumber] end[RaNumber]
2xRootDec dec
#
#nominalized numbers
#
singles DatExceptions[-OblPlAlu] negSuffix[pst]?
DaSuffixedNums DatExceptions[-OblPlAlu] negSuffix[pst]?
( NomNumbersAgr | NomNumbersNonAgr ) CaseMain[-nullNom,-RegDat,ErgR] negSuffix[pst]?
NomNumbersAgr genitiveCase[GenAgr] negSuffix[pst]?
NomNumbersNonAgr genitiveCase[-GenAgr] negSuffix[pst]?
#
#adjective agreement
#
agrInitial adjDouble agrFinal # can agree with 2 heads, see Kibrik et al.
adjSingle agrFinal
#
#pronoun declension
#
PerPronounStems genitiveCase[GenAgr] negSuffix[pst]?
PerPronounStems[PrExc] CaseMain[ErgR,-RegErg,-nullNom] negSuffix[pst]?
PerPronounStems[-PrExc] CaseMain[ErgR] negSuffix[pst]?
PerPronounExc negSuffix[pst]?

# ----------------------------------------------------------------------------------------------
# ----------------------------------------------------------------------------------------------
# LEXICON ENTRIES
#
# COMMON MARKERS
#
LEXICON negSuffix
<neg>:чӀи[pst]
<neg>:чӀе[fut]

# -----------------------------------------------------------------------------------------------
# NUMBER DERIVATION
# 
LEXICON end
<num>:да[DaNumber]
<num>:ра[RaNumber]

LEXICON col
<col>:ла

# number modifiers
# 
LEXICON ordinal
<ord>:ло[ser1]
<ord>:ла[ser2]

LEXICON dec
<*>гьацӀа<num><ten>:ацӀа

LEXICON conjDec
<*>гьацӀа<num><ten>:ацӀ # 21-29

LEXICON cent
<*>бешенда<num><hundred>:бешен

LEXICON mil
<*>азарда<num><thousand>:азар

# distributional number modifiers
#
LEXICON decDist
цев<num><distr>:гьацӀигь

LEXICON centDist
<distr>:беши

LEXICON milDist
<distr>:ази

# number links
# 
LEXICON decComp
<lnk>:л # 11-19

LEXICON conjCentMil
<lnk>:чӀекӀуб

# --------------------------------------------------------------------------------------------
# NOMINAL INFLECTION
# Oblique stem markers
#
LEXICON OblSingular
<sg><obl>:[OblZero]
<sg><obl>:а[OblA]
<sg><obl>:и[OblI]
<sg><obl>:е[OblE]
<sg><obl>:у[OblU]
<sg><obl>:щу[OblSu]
<sg><obl>:лу[OblLu]
<sg><obl>:лъи[OblLi]

LEXICON OblPlural
<obl>:а[OblPlA]
<obl>:алу[OblPlAlu]
<obl>:лу[OblPlLu]

# Noun plural suffixes
#
LEXICON PlAbs
<nom>:и

LEXICON PlMain
<pl>:{б}[PlBi]
<pl>:дар[PlDari]

# ----------------------------------------------------------------------------------------------
# CASE MARKERS
LEXICON CaseMain
<nom>:[ErgD,ErgR,nullNom]
<dat>:ла[ErgD,ErgR,RegDat]
<erg>:д[ErgD]
<erg>:р[ErgR,RegErg]
<aff>:ба[ErgD,ErgR]
<subst>:бас[ErgD,ErgR]

LEXICON DatExceptions
<sg><obl><dat>:ща[ErgD,ErgR,OblSu]
<sg><obl><dat>:лъа[ErgD,ErgR,OblLi]
<pl><obl><dat>:лла[ErgD,ErgR,OblPlAlu]
<pl><obl><dat>:ла[ErgD,ErgR,OblPlLu]

LEXICON genitiveCase
<gen>:лъ
<gen><M><sg>:в[GenAgr]
<gen><F><sg>:й[GenAgr]
<gen><N><sg>:б[GenAgr]
<gen><H><pl>:ба[GenAgr]
<gen><N><pl>:р[GenAgr]

# ---------------------------------------------------------------------------------------------
# AGREEMENT MARKERS
LEXICON agrInitial
:в
:й
:б
:р

LEXICON agrMiddle
<M><sg>:в
<F><sg>:й
<N><sg>:{б}
<H><pl>:{р}
<N><pl>:{р}

LEXICON agrFinal
<M><sg>:в
<F><sg>:й
<N><sg>:{б}
<H><pl>:{б}а
<N><pl>:{р}

# ---------------------------------------------------------------------------------------------
# ROOTS
# number roots
# 
LEXICON rootOne # 1
цев<num>:це[norm]
цев<num><dist>:цеце[red]

LEXICON rootInit
чӀеда<num>:чӀ{е}[DaNumber,RaNumber]
гьабда<num>:гьаб[DaNumber]
буъуда<num>:буъу[DaNumber,RaNumber]
иᵸщтуда<num>:иᵸщту[DaNumber,RaNumber]
инлӀида<num>:инлӀи[DaNumber,RaNumber]
гьакьуда<num>:гьакьу[DaNumber,RaNumber]
бикьида<num>:бикьи[DaNumber,RaNumber]
гьачӀвада<num>:гьачӀва[DaNumber,RaNumber]

LEXICON rootDec
цев<num>:гь
чӀеда<num>:чӀ{е} # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
иᵸщтуда<num>:иᵸщвд # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀв # 9

LEXICON rootCent
цев<num>:
чӀеда<num>:чӀ{е} # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
иᵸщтуда<num>:иᵸщв # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀа # 9

LEXICON 2xRootInit
чӀеда<num><dist>:чӀечӀе[excl]
гьабда<num><dist>:гьабдигьаб[DaNumber]
буъуда<num><dist>:бубуъу[DaNumber,RaNumber]
иᵸщтуда<num><dist>:иᵸщтуиᵸщту[DaNumber,RaNumber]
инлӀида<num><dist>:инлӀиъинлӀи[DaNumber,RaNumber]
гьакьуда<num><dist>:гьакьугьакьу[DaNumber,RaNumber]
бикьида<num><dist>:бикьибикьи[DaNumber,RaNumber]
гьачӀвада<num><dist>:гьачӀигьачӀва[DaNumber,RaNumber]

LEXICON 2xRootDec
чӀеда<num><dist>:чӀечӀ{е} # 2
гьабда<num><dist>:гьабигьаб # 3
буъуда<num><dist>:бубуъ # 4
иᵸщтуда<num><dist>:иᵸщтуиᵸщвд # 5
инлӀида<num><dist>:инлӀиъинлӀ # 6
гьакьуда<num><dist>:гьакьугьакьв # 7
бикьида<num><dist>:бикьибикь # 8
гьачӀвада<num><dist>:гьачӀигьачӀв # 9

# noun roots
#
LEXICON nounSu
има<noun>:има # father

LEXICON nounLi
гьакӀуй<noun>:гьакӀуй # woman
гьезеб<noun>:гьезеб # stock

# adjective roots
#
LEXICON adjDouble
вагьарув<adjective>:агьару

LEXICON adjSingle
гьацӀав<adjective>:гьацӀа

#pronouns
#
LEXICON PerPronounStems
де<pronoun><sg><1P>:ди[PrExc]
ме<pronoun><sg><2P>:ду[PrExc]
ищи<pronoun><pl><1P>:ищи
инлӀи<pronoun><pl><1P>:инлӀи
бишти<pronoun><pl><2P>:бишти

LEXICON PerPronounExc
де<pronoun><sg><1P><nom>:де
де<pronoun><sg><1P><erg>:ден
ме<pronoun><sg><2P><nom>:ме
ме<pronoun><sg><2P><erg>:мен