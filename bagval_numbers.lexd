PATTERN decimal
rootDec dec

PATTERN centi
rootCent cent

PATTERN milli
rootCent mil

PATTERN singles
rootInit[DaNumber] end[DaNumber]
rootInit[RaNumber] end[RaNumber] # ра, Кибрик et al. С. 155

PATTERNS
rootOne agrSingular
singles
decimal # аца
decimal end[DaNumber] # ацада
centi # бешен
centi end[DaNumber] # бешенда
milli end[DaNumber] # азарда
#forms with links
decimal decComp # ацал, link in 11-19
rootDec conjDec # ац, link in 21-29
centi conjCentMil  
milli conjCentMil # links in 101-199, 10001-1999
#collective forms
singles col 
decimal col 
decimal end[DaNumber] col 
centi end[DaNumber] col 
milli end[DaNumber] col # collective

#
# suffixes

LEXICON end
<num>:да[DaNumber]
<num>:ра[RaNumber]

LEXICON col
<col>:ла

#
# agreement markers

LEXICON agrSingular
<M><sg>:в
<F><sg>:й
<N><sg>:б

#LEXICON agrPlural
#<H><pl>:{б}а
#<N><pl>:{р}

#
# modifiers

LEXICON dec
<*>гьаца<num>:ацӀа

LEXICON conjDec
<*>гьаца<num>:ацӀ # 21-29

LEXICON cent
<*>бешенда<num><hundred>:бешен

LEXICON mil
<*>азарда<num><thousand>:азар

#
# links

LEXICON decComp
<lnk>:л # 11-19

LEXICON conjCentMil
<lnk>:чӀекӀуб

#
# roots

LEXICON rootOne
цев<num>:це # 1

LEXICON rootInit
чӀеда<num>:чӀе[DaNumber,RaNumber]
гьабда<num>:гьаб[DaNumber]
буъуда<num>:буъу[DaNumber,RaNumber]
инщтуда<num>:инщту[DaNumber,RaNumber]
инлӀида<num>:инлӀи[DaNumber,RaNumber]
гьакьуда<num>:гьакьу[DaNumber,RaNumber]
бикьида<num>:бикьи[DaNumber,RaNumber]
гьачӀвада<num>:гьачӀва[DaNumber,RaNumber]

LEXICON rootDec
цев<num>:гь
чӀеда<num>:чӀ # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
инщтуда<num>:инщвд # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀв # 9

LEXICON rootCent
цев<num>:
чӀеда<num>:чӀе # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
инщтуда<num>:инщв # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀа # 9
