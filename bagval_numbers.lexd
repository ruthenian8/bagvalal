PATTERN decimal
rootDec dec

PATTERN centi
rootCent cent

PATTERN milli
rootCent mil

PATTERN singles
rootInit[DaNumber] end[DaNumber]
rootInit[RaNumber] end[RaNumber] # ра, Кибрик et al. С. 155

PATTERN DaSuffixedNums
decimal end[DaNumber]
milli end[DaNumber]
centi end[DaNumber]

PATTERN NomNumbersAgr
singles ( OblSingular[OblSu] | OblPlural[OblPlLu] )
DaSuffixedNums ( OblSingular[OblSu] | OblPlural[OblPlLu] )

PATTERN NomNumbersNonAgr
singles ( OblSingular[OblLi] | OblPlural[OblPlLi] )
DaSuffixedNums ( OblSingular[OblLi] | OblPlural[OblPlLi] )

PATTERN OrdNumbersSer1
singles [ла]? ordinal[ser1] 
DaSuffixedNums [ла]? ordinal[ser1]

PATTERN OrdNumbersSer2
singles [ла] ordinal[ser2] 
DaSuffixedNums [ла] ordinal[ser2]

PATTERN OblAdjAgr
agrFinal[Msg] OblSingular[OblSu]
agrFinal[Hpl] OblPlural[OblPlLu]

PATTERN OblAdjNonAgr
agrFinal[Fsg] OblSingular[OblLi]
agrFinal[Nsg] OblSingular[OblLi]
agrFinal[Npl] OblPlural[OblPlLi]

PATTERN OblAdjDative
agrFinal[Msg] DatExceptions[OblSu]
agrFinal[Fsg] DatExceptions[OblLi]
agrFinal[Nsg] DatExceptions[OblLi]
agrFinal[Hpl] DatExceptions[OblPlLu]
agrFinal[Npl] DatExceptions[OblLi]

# ----------------------------------------------------------------------------------------------
# ----------------------------------------------------------------------------------------------
# MAIN PATTERNS
PATTERNS
# regular numbers
#
rootOne[norm] agrFinal
singles
decimal # аца
centi # бешен
DaSuffixedNums
#
#forms with links
#
decimal decComp # ацал, link in 11-19
rootDec conjDec # ац, link in 21-29
centi conjCentMil  
milli conjCentMil # links in 101-199, 10001-1999
#
#nominalized cardinal numbers
#
singles DatExceptions[-OblPlAlu] negSuffix[pst]?
DaSuffixedNums DatExceptions[-OblPlAlu] negSuffix[pst]?
( NomNumbersAgr | NomNumbersNonAgr ) CaseMain[-nullNom,-RegDat,ErgR] negSuffix[pst]?
NomNumbersAgr genitiveCase[GenAgr] negSuffix[pst]?
NomNumbersNonAgr genitiveCase[-GenAgr] negSuffix[pst]?
#
#collective numbers
#
singles col 
decimal col 
DaSuffixedNums col
#
#ordinal numbers
#
rootOne[ord]
OrdNumbersSer1 agrFinal
OrdNumbersSer2
#
#nominalized ordinals
#
OrdNumbersSer1 agrFinal CaseMain[nullNom] negSuffix[pst]?
OrdNumbersSer1 OblAdjAgr CaseMain[-nullNom,ErgR] negSuffix[pst]?
OrdNumbersSer1 OblAdjAgr genitiveCase[GenAgr] negSuffix[pst]?
OrdNumbersSer1 OblAdjNonAgr CaseMain[-nullNom,ErgR] negSuffix[pst]?
OrdNumbersSer1 OblAdjNonAgr genitiveCase[-GenAgr] negSuffix[pst]?
OrdNumbersSer1 OblAdjDative negSuffix[pst]?
#
#distributional numbers
#
rootOne[red] agrFinal # цецеб = 1
rootCent centDist cent end[DaNumber]
rootCent milDist mil end[DaNumber]?
decDist dec ( end[DaNumber] | end[RaNumber] ) # гьабдигьабда = 10
2xRootInit[excl] # 2
2xRootInit[-excl,DaNumber] end[DaNumber]
2xRootInit[-excl,RaNumber] end[RaNumber]
2xRootDec dec
#
#adjective agreement
#
agrInitial adjDouble agrFinal # can agree with 2 heads, see Kibrik et al.
adjSingle agrFinal
#
#nominalized adjectives
#double
agrInitial[Msg] adjDouble agrFinal[Msg] ( CaseMain[nullNom] | DatExceptions[OblSu] )
agrInitial[Fsg] adjDouble agrFinal[Fsg] ( CaseMain[nullNom] | DatExceptions[OblLi] )
agrInitial[Nsg] adjDouble agrFinal[Nsg] ( CaseMain[nullNom] | DatExceptions[OblLi] )
agrInitial[Hpl] adjDouble agrFinal[Hpl] ( CaseMain[nullNom] | DatExceptions[OblPlLu] )
agrInitial[Npl] adjDouble agrFinal[Npl] ( CaseMain[nullNom] | DatExceptions[OblLi] )
agrInitial[Msg] adjDouble agrFinal[Msg] OblSingular[OblSu] ( CaseMain[-nullNom,ErgR] | genitiveCase[GenAgr] )
agrInitial[Fsg] adjDouble agrFinal[Fsg] OblSingular[OblLi] ( CaseMain[-nullNom,ErgR] | genitiveCase[-GenAgr] )
agrInitial[Nsg] adjDouble agrFinal[Nsg] OblSingular[OblLi] ( CaseMain[-nullNom,ErgR] | genitiveCase[-GenAgr] )
agrInitial[Hpl] adjDouble agrFinal[Hpl] OblPlural[OblPlLu] ( CaseMain[-nullNom,ErgR] | genitiveCase[GenAgr] )
agrInitial[Npl] adjDouble agrFinal[Npl] OblPlural[OblPlLi] ( CaseMain[-nullNom,ErgR] | genitiveCase[-GenAgr] )
#single
adjSingle agrFinal CaseMain[nullNom]
adjSingle OblAdjAgr ( CaseMain[-nullNom,ErgR] | genitiveCase[GenAgr] )
adjSingle OblAdjNonAgr ( CaseMain[-nullNom,ErgR] | genitiveCase[-GenAgr] )
adjSingle OblAdjDative negSuffix[pst]?
#
#pronoun declension
#
PerPronounStems genitiveCase[GenAgr] negSuffix[pst]?
PerPronounStems[PrExc] CaseMain[ErgR,-RegErg,-nullNom] negSuffix[pst]?
PerPronounStems[-PrExc] CaseMain[ErgR] negSuffix[pst]?
PerPronounExc negSuffix[pst]?

# ----------------------------------------------------------------------------------------------
# ----------------------------------------------------------------------------------------------
# LEXICON ENTRIES
#
# COMMON MARKERS
#
LEXICON negSuffix
<neg>:чӀи[pst]
<neg>:чӀе[fut]

# -----------------------------------------------------------------------------------------------
# NUMBER DERIVATION
# 
LEXICON end
<num>:да[DaNumber]
<num>:ра[RaNumber]

LEXICON col
<col>:ла

# number modifiers
# 
LEXICON ordinal
<ord>:ло[ser1]
<ord>:ла[ser2]

LEXICON dec
<*>гьацӀа<num><ten>:ацӀа

LEXICON conjDec
<*>гьацӀа<num><ten>:ацӀ # 21-29

LEXICON cent
<*>бешенда<num><hundred>:бешен

LEXICON mil
<*>азарда<num><thousand>:азар

# distributional number modifiers
#
LEXICON decDist
цев<num><distr>:гьацӀигь

LEXICON centDist
<distr>:беши

LEXICON milDist
<distr>:ази

# number links
# 
LEXICON decComp
<lnk>:л # 11-19

LEXICON conjCentMil
<lnk>:чӀекӀуб

# --------------------------------------------------------------------------------------------
# NOMINAL INFLECTION
# Oblique stem markers
#
LEXICON OblSingular
<obl>:[OblZero]
<obl>:а[OblA]
<obl>:и[OblI]
<obl>:е[OblE]
<obl>:у[OblU]
<obl>:щу[OblSu]
<obl>:лу[OblLu]
<obl>:лъи[OblLi]

LEXICON OblPlural
<obl>:а[OblPlA]
<obl>:алу[OblPlAlu]
<obl>:лу[OblPlLu]
<obl>:лъи[OblPlLi]

# Noun plural suffixes
#
LEXICON PlAbs
<nom>:и

LEXICON PlMain
<pl>:{б}[PlBi]
<pl>:дар[PlDari]

# ----------------------------------------------------------------------------------------------
# CASE MARKERS
LEXICON CaseMain
<nom>:[ErgD,ErgR,nullNom]
<dat>:ла[ErgD,ErgR,RegDat]
<erg>:д[ErgD]
<erg>:р[ErgR,RegErg]
<aff>:ба[ErgD,ErgR]
<subst>:бас[ErgD,ErgR]

LEXICON DatExceptions
<sg><obl><dat>:ща[ErgD,ErgR,OblSu]
<sg><obl><dat>:лъа[ErgD,ErgR,OblLi]
<pl><obl><dat>:лла[ErgD,ErgR,OblPlAlu]
<pl><obl><dat>:ла[ErgD,ErgR,OblPlLu]

LEXICON genitiveCase
<gen>:лъ
<gen><M><sg>:в[GenAgr]
<gen><F><sg>:й[GenAgr]
<gen><N><sg>:б[GenAgr]
<gen><H><pl>:ба[GenAgr]
<gen><N><pl>:р[GenAgr]

# ---------------------------------------------------------------------------------------------
# AGREEMENT MARKERS
LEXICON agrInitial
:в[Msg]
:й[Fsg]
:б[Nsg,Hpl]
:р[Npl]

LEXICON agrMiddle
<M><sg>:в
<F><sg>:й
<N><sg>:{б}
<H><pl>:{р}
<N><pl>:{р}

LEXICON agrFinal
<M><sg>:в[Msg]
<F><sg>:й[Fsg]
<N><sg>:{б}[Nsg]
<H><pl>:{б}а[Hpl]
<N><pl>:{р}[Npl]

# ---------------------------------------------------------------------------------------------
# ROOTS
# number roots
# 
LEXICON rootOne # 1
цев<num>:це[norm]
цев<num><dist>:цеце[red]
цев<num><ord>:гьечӀисес[ord]

LEXICON rootInit
чӀеда<num>:чӀ{е}[DaNumber,RaNumber]
гьабда<num>:гьаб[DaNumber]
буъуда<num>:буъу[DaNumber,RaNumber]
иᵸщтуда<num>:иᵸщту[DaNumber,RaNumber]
инлӀида<num>:инлӀи[DaNumber,RaNumber]
гьакьуда<num>:гьакьу[DaNumber,RaNumber]
бикьида<num>:бикьи[DaNumber,RaNumber]
гьачӀвада<num>:гьачӀва[DaNumber,RaNumber]

LEXICON rootDec
цев<num>:гь
чӀеда<num>:чӀ{е} # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
иᵸщтуда<num>:иᵸщвд # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀв # 9

LEXICON rootCent
цев<num>:
чӀеда<num>:чӀ{е} # 2
гьабда<num>:гьаб # 3
буъуда<num>:буъ # 4
иᵸщтуда<num>:иᵸщв # 5
инлӀида<num>:инлӀ # 6
гьакьуда<num>:гьакьв # 7
бикьида<num>:бикь # 8
гьачӀвада<num>:гьачӀа # 9

LEXICON 2xRootInit
чӀеда<num><dist>:чӀечӀе[excl]
гьабда<num><dist>:гьабдигьаб[DaNumber]
буъуда<num><dist>:бубуъу[DaNumber,RaNumber]
иᵸщтуда<num><dist>:иᵸщтуиᵸщту[DaNumber,RaNumber]
инлӀида<num><dist>:инлӀиъинлӀи[DaNumber,RaNumber]
гьакьуда<num><dist>:гьакьугьакьу[DaNumber,RaNumber]
бикьида<num><dist>:бикьибикьи[DaNumber,RaNumber]
гьачӀвада<num><dist>:гьачӀигьачӀва[DaNumber,RaNumber]

LEXICON 2xRootDec
чӀеда<num><dist>:чӀечӀ{е} # 2
гьабда<num><dist>:гьабигьаб # 3
буъуда<num><dist>:бубуъ # 4
иᵸщтуда<num><dist>:иᵸщтуиᵸщвд # 5
инлӀида<num><dist>:инлӀиъинлӀ # 6
гьакьуда<num><dist>:гьакьугьакьв # 7
бикьида<num><dist>:бикьибикь # 8
гьачӀвада<num><dist>:гьачӀигьачӀв # 9

# noun roots
#
LEXICON nounSu
има<noun>:има # father

LEXICON nounLi
гьакӀуй<noun>:гьакӀуй # woman
гьезеб<noun>:гьезеб # stock

# adjective roots
#
LEXICON adjDouble
вагьарув<adjective>:агьару

LEXICON adjSingle
гьацӀав<adjective>:гьацӀа

#pronouns
#
LEXICON PerPronounStems
де<pronoun><sg><1P>:ди[PrExc]
ме<pronoun><sg><2P>:ду[PrExc]
ищи<pronoun><pl><1P>:ищи
инлӀи<pronoun><pl><1P>:инлӀи
бишти<pronoun><pl><2P>:бишти

LEXICON PerPronounExc
де<pronoun><sg><1P><nom>:де
де<pronoun><sg><1P><erg>:ден
ме<pronoun><sg><2P><nom>:ме
ме<pronoun><sg><2P><erg>:мен